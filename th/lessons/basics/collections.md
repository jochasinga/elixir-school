---
layout: page
title: Collections
category: basics
order: 2
lang: th
---

List, tuples, keywords, maps และ functional combinators

{% include toc.html %}

## ลิสค์ (Lists)

ลิสต์ คือ ลำดับข้อมูลที่ถูกรวบรวมเข้าด้วยกัน โดยลำดับสามารถเก็บค่าหลายๆ ชนิดได้ และ สามารถเก็บค่าซ้ำกันได้:

```elixir
iex> [3.41, :pie, "Apple"]
[3.41, :pie, "Apple"]
```

ลิสต์ใน Elixir เป็นลิสต์เชื่อม (Linked lists) นั่นหมายความว่าการหาจำนวนข้อมูลภายในลิสต์จะเป็นการปฏิบัติการแบบ `O(n)` ด้วยเหตุผลเดียวกัน การเพิ่มค่าตรงต้นลิสต์มักจะเร็วกว่าการเพิ่มต่อท้ายลิสต์:

```elixir
iex> list = [3.41, :pie, "Apple"]
[3.41, :pie, "Apple"]
iex> ["π"] ++ list
["π", 3.41, :pie, "Apple"]
iex> list ++ ["Cherry"]
[3.41, :pie, "Apple", "Cherry"]
```


### การต่อลิสต์ (List Concatenation)

การต่อลิสต์สามารถทำได้ด้วยเครื่องหมายปฏิบัติการ `++/2`:

```elixir
iex> [1, 2] ++ [3, 4, 1]
[1, 2, 3, 4, 1]
```

### การตัดลิสต์ (List Subtraction)

การตัดลิสต์สามารถทำได้ด้วยเครื่องหมายปฏิบัติการ `--/2` เราสามารถตัดค่าที่ไม่มีอยู่ในลิสต์ต้นได้โดยปลอดภัย:

```elixir
iex> ["foo", :bar, 42] -- [42, "bar"]
["foo", :bar]
```

**หมายเหตุ:** การตัดลิสต์ใน [การเปรียบเทียบแบบเข้มงวด](../basics/#comparison) ในการ match ค่าต่างๆ ในลิสต์

### หัว / ท้าย

ในการใช้งานลิสต์ มักจะพบเจอการทำงานกับ หัว และ ท้าย ของลิสต์ได้บ่อยๆ หัว คือ สมาชิกแรกของลิสต์ ส่วน หาง คือ สมาชิกที่เหลือของลิสต์ Elixir มี methods `hd` และ `tl` สำหรับการนี้โดยเฉพาะ

```elixir
iex> hd [3.41, :pie, "Apple"]
3.41
iex> tl [3.41, :pie, "Apple"]
[:pie, "Apple"]
```

นอกเหนือจาก functions ที่ได้กล่าวมา นายยังสามารถใช้ [การจับคู่รูปแบบ](../pattern-matching/) และเครื่องหมาย `|` สำหรับการแบ่งลิสต์ออกเป็นส่วนหัว และส่วนหาง เราจะได้เรียนเกี่ยวกับรูปแบบดังกล่าวในบทต่อๆ ไป

```elixir
iex> [h|t] = [3.41, :pie, "Apple"]
[3.41, :pie, "Apple"]
iex> h
3.41
iex> t
[:pie, "Apple"]
```

## ทูเปิ้ล (Tuples)

ทูเปิ้ลคล้ายกับลิสต์ต่างกันที่ทูเปิ้ลเก็บค่าต่างๆ ต่อเนื่องกันในหน่วยความจำ นั่นทำให้การนับจำนวนสมาชิกสามารถทำได้เร็ว แต่การเปลี่ยนค่าของสมาชิกค่อนข้างเปลืองทรัพยากร เนื่องจากต้องลอกทูเปิ้ลเดิมเพื่อสร้างอันใหม่ทั้งหมด เราสามารถสร้างทูเปิ้ลได้ด้วยเครื่องหมาย `{` และ  `}`:

```elixir
iex> {3.41, :pie, "Apple"}
{3.41, :pie, "Apple"}
```

เป็นที่นิยมในการใช้ทูเปิ้ลเพื่อรีเทิร์นข้อมูลเพิ่มเติมจากฟังค์ชั่น ประโยชน์ของวิธีการนี้จะชัดเจนมากขึ้นเมื่อเราถึงบทเกี่ยวกับ การจับคู่รูปแบบ:

```elixir
iex> File.read("path/to/existing/file")
{:ok, "... contents ..."}
iex> File.read("path/to/unknown/file")
{:error, :enoent}
```

## ลิสต์คีย์เวิร์ด (Keyword lists)

คีย์เวิร์ด และ แมป เป็น ชุดข้อมูลแบบ associative ของภาษา Elixir ลิสต์คีย์เวิร์ด คือ ลิสต์ชนิดพิเศษที่เก็บทูเปิ้ลที่ค่าแรกเป็นอะตอม ลิสต์คีย์เวิร์ดมีประสิทธิภาพเท่ากับ ลิสต์:

```elixir
iex> [foo: "bar", hello: "world"]
[foo: "bar", hello: "world"]
iex> [{:foo, "bar"}, {:hello, "world"}]
[foo: "bar", hello: "world"]
```

คุณลักษณะสามอย่างของลิสต์คีเวิร์ด คือ:

+ คีย์เป็นอะตอม
+ คีย์มีการจัดลำดับ
+ คีย์สามารถซ้ำกันได้

ด้วยเหตุผลเหล่านี้ ลิสต์คีย์เวิร์ดจึงมักถูกใช้เพื่อส่ง ออปชั่นต่างๆ ให้ฟังค์ชั่น

## แมป (Maps)

ใน Elixir แมปเป็นวีธีการเก็บข้อมูลแบบ key-value ที่สะดวกที่สุด ต่างกับ ลิสต์คีย์เวิร์ด คีย์ในแมปสามารถเป็นชนิดใดก็ได้และไม่มีการเรียงลำดับ นายสามารถสร้างแมปด้วยเครื่องหมาย `%{}`:

```elixir
iex> map = %{:foo => "bar", "hello" => :world}
%{:foo => "bar", "hello" => :world}
iex> map[:foo]
"bar"
iex> map["hello"]
:world
```

Elixir เวอร์ชั่น 1.2 เป็นต้นไป เราสามารถใช้ตัวแปรเป็นคีย์ในแมปได้ด้วย:

```elixir
iex> key = "hello"
"hello"
iex> %{key => "world"}
%{"hello" => "world"}
```

ถ้าคีย์ที่ซ้ำกันถูกเพิ่มเข้าไปในแมป คีย์นั้นจะแทนที่ค่าของคีย์เดิมทันที:

```elixir
iex> %{:foo => "bar", :foo => "hello world"}
%{foo: "hello world"}
```

As we can see from the output above, there is a special syntax for maps containing only atom keys:

```elixir
iex> %{foo: "bar", hello: "world"}
%{foo: "bar", hello: "world"}

iex> %{foo: "bar", hello: "world"} == %{:foo => "bar", :hello => "world"}
true
```

คุณสมบัติสำคัญอีกอย่างของแมป คือ แมปมีรูปแบบการอัฟเดทและเข้าถึงคีย์ชนิดอะตอมโดยเฉพาะ:

```elixir
iex> map = %{foo: "bar", hello: "world"}
%{foo: "bar", hello: "world"}
iex> %{map | foo: "baz"}
%{foo: "baz", hello: "world"}
iex> map.hello
"world"
```
